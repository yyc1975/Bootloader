#
# tools/gdb/gdbinit-rvi
#
# Copyright (c) 2010, Maxim Integrated Products
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#     * Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#     * Neither the name of the Maxim Integrated Products nor the
#       names of its contributors may be used to endorse or promote products
#       derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY MAXIM INTEGRATED PRODUCTS ''AS IS'' AND ANY
# EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL MAXIM INTEGRATED PRODUCTS BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

set verbose off
set confirm off
set logging overwrite on
set logging file debug.log
set logging on
set arm fpu softfpa

b __handler_undefined_
b __handler_swi_
b __handler_prefetch_
b __handler_abort_
b __handler_irq_
b __handler_firq_
b __exit

b *(__handler_prefetch_+16)
b *(__handler_abort_+16)

commands 1
	silent
	printf "============================================================\n"
	printf "** ERROR: UNDEFINED EXCEPTION **\n"
	printf "------------------------------------------------------------\n"
	error
end

commands 2
	silent
	printf "============================================================\n"
	printf "** ERROR: SWI EXCEPTION **\n"
	printf "------------------------------------------------------------\n"
	error
end

commands 3
	silent
	printf "============================================================\n"
	printf "** ERROR: PREFETCH EXCEPTION **\n"
	printf "------------------------------------------------------------\n"
	printf "TRY TO ACCESS STACK: "
	print /x *((unsigned *) $sp)
	printf "------------------------------------------------------------\n"
	continue
end

commands 4
	silent
	printf "============================================================\n"
	printf "** ERROR: ABORT EXCEPTION **\n"
	printf "------------------------------------------------------------\n"
	printf "TRY TO ACCESS STACK: "
	print /x *((unsigned *) $sp)
	printf "------------------------------------------------------------\n"
	continue
end

commands 5
	silent
	printf "============================================================\n"
	printf "** ERROR: IRQ EXCEPTION **\n"
	printf "============================================================\n"
	exit
end

commands 6
	silent
	printf "============================================================\n"
	printf "** ERROR: FIQ EXCEPTION **\n"
	printf "============================================================\n"
	exit
end

commands 7
	silent
	printf "[INFO] Execution end\n"
	exit
end

commands 8
	silent
	io_error
end

commands 9
	silent
	io_error
end

define io_error
	probe_error
	printf "FAULT ADDRESS REGISTER: 0x%08x\n",$r1
	printf "FAULT STATUS REGISTER: 0x%08x\n",$r0
	printf "FSR status field encoding\n"
	printf "------------------------------------------------------------\n"
	printf "Source                        Size            Status Domain\n"
	printf "Alignment                                     b00x1  Invalid\n"
	printf "External abort on translation First level     b1100  Invalid\n"
	printf "                              Second level    b1110  Valid\n"
	printf "Translation                   Section         b0101  Invalid\n"
	printf "                              Page            b0111  Valid\n"
	printf "Domain                        Section         b1001  Valid\n"
	printf "                              Page            b1011  Valid\n"
	printf "Permission                    Section         b1101  Valid\n"
	printf "                              Page            b1111  Valid\n"
	printf "External abort                Section or page b10x0  Invalid\n"
	printf "------------------------------------------------------------\n"
	error
end

define exit
	shell empty -k
	shell pkill empty
	probe_reset
	quit
end

define error
	printf "ADDRESS: "
	print /x ($lr-4)
	printf "------------------------------------------------------------\n"
	disassemble /m $lr-36,$lr+32
	printf "------------------------------------------------------------\n"
	info all-registers
	printf "============================================================\n"
	exit
end

printf "[INFO] Connection to the probe\n"
probe_remote

printf "[INFO] Reset of the Core\n"
probe_reset

printf "[INFO] Load\n"
load

printf "[INFO] Cp15 Control Register\n"
probe_show_cr

printf "[INFO] Telnet connection\n"
shell empty -f -L telnet.log telnet 10.201.4.12 5000

printf "[INFO] Continue execution\n"
continue
